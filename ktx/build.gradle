plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

group = 'com.github.eastar-dev'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 15
        versionName '2.0.0'
    }
    lintOptions {
        abortOnError false
    }
    buildFeatures.dataBinding = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.21"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.lifecycle:lifecycle-process:2.3.1'
}

//https://developer.android.com/studio/build/maven-publish-plugin
//run gradle publishToMavenLocal

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'dev.eastar.ktx'
                artifactId = 'eastarktx'
                version = '2.0.0'
                pom {
                    name = 'EastarKtx'
                    description = 'android usefull simple util for Alert, Context, Intent, etc...'
                    url = 'https://github.com/eastar-dev/EastarKtx'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'eastar'
                            name = 'eastar Jeong'
                            email = 'eastarj@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/eastar-dev/EastarKtx.git'
                        developerConnection = 'scm:git:ssh://git@github.com/eastar-dev/EastarKtx'
                        url = 'https://github.com/eastar-dev/EastarKtx'
                    }
                }
            }
        }
    }
}
